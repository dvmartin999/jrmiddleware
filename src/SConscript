 #***********************************************************************
 #* @file      SConscript
 #* @author    Dave Martin, DeVivo AST, Inc.  
 #* @date      2008/03/03
 #*
 #*  Copyright (C) 2008. DeVivo AST, Inc
 #*
 #*	 This program is free software: you can redistribute it and/or modify  it 
 #*  under the terms of the Jr Middleware Open Source License which can be 
 #*  found at http://www.jrmiddleware.com/osl.html.  This program is 
 #*  distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
 #*  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
 #*  PARTICULAR PURPOSE.  See the Jr Middleware Open Source License for more 
 #*  details.
 #*	
 #*  For more information, please contact DeVivo AST at info@devivoast.com
 #*  or by mail at 2225 Drake Ave, Suite 2, Huntsville, AL  35805.
 #*
 #*  The Jr Middleware Open Source License does not permit incorporating your 
 #*  program into proprietary programs. If this is what you want to do, 
 #*  use the Jr Middleware Commercial License. More information can be 
 #*  found at: http://www.jrmiddleware.com/licensing.html.
 #************************************************************************
 
# Import base environment
import os
Import( 'baseEnv' )

# Here's where the good stuff starts...
API_Files = [
    'JuniorAPI.cpp',
    'JrSockets.cpp',
    'JuniorMgr.cpp',
    'OS.cpp',
    'JrLogger.cpp',
    'JUDPTransport.cpp',
    'JSerial.cpp',
    'ChecksumCRC.cpp',
    'TCPConnection.cpp',
    'JTCPTransport.cpp',
    'tinyxml/tinyxml.cpp',
    'tinyxml/tinyxmlerror.cpp',
    'tinyxml/tinyxmlparser.cpp'
]

# Define multiple environments for building and using
# the library as a Shared Library (dll or so).
dllenv = baseEnv.Copy()
dllenv.Append( CCFLAGS = ['-D__BUILD_DLL__'] ) 
dllenv.Append( LINKFLAGS = ['/DEF:Junior.DEF'] )
dllenv['SHOBJPREFIX'] = 'sh'
ocuenv = baseEnv.Copy()
ocuenv.Append( CCFLAGS = ['-D__USE_DLL__'] )                 

# On Windows, we generate a DLL for the API
# as well as a executable for the Run-Time Engine.
# The RTE must be linked to a static library, however,
# so we need to build both a static and a shared library.
# These are given different names to prevent confusion,
# but only the DLL (and associated lib) are intended 
# for release.
if os.name == "nt":

    apilib = dllenv.SharedLibrary( target = 'Junior',
                                   source = API_Files )
                                                                
    rteapp = baseEnv.Program( target = 'JuniorRTE',
							  source = ['JuniorRTE.cpp'] + API_Files )
							  
    baseEnv.Install( baseEnv['INSTALL_BINDIR'], 'Junior.dll' )							  
						
else:

    apilib = baseEnv.Library(  target = 'Junior',
                               source = API_Files )
                           
    rteapp = baseEnv.Program( target = 'JuniorRTE',
							  source = 'JuniorRTE.cpp',
							  LIBS = 'Junior' )
                           

baseEnv.Install( baseEnv['INSTALL_LIBDIR'], apilib )
baseEnv.Install( baseEnv['INSTALL_BINDIR'], rteapp )
baseEnv.Install( baseEnv['INSTALL_BINDIR'], 'jr_config.xml' )